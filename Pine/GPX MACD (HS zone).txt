//@version=5
//-------------------------------------------------------------------------------------------------------------------------------------------
//  Script Name:    GPX-I-MACD v2.0 Indicator (with N x StdDeviation)
//  Ideal      :    Custom version of MACD for use with GPX Strategies
//  Author     :    Guy Pedashenko      Original creation date: 15-Jun-24       Latest version date: 02-Jul-24
//  Version    :    v2.11
//  Description:    MACD crossing.  N x Std Devation band lines (can adjust variable for Std Dev multiplier.)
//                                                                       
//-------------------------------------------------------------------------------------------------------------------------------------------// 80% defval
indicator(title="GPX-I-MACD V2.11", shorttitle="GPX-MACD", timeframe="", timeframe_gaps=true)

NumBars = input.int(title = "Number of Bars",  minval = 100, maxval = 4000, defval = 2000)    
nStdDevs = input.float(title = "Number of STD Deviations",  minval = 1, maxval = 10, defval = 2)    
// Getting inputs
fast_length = input(title = "Fast Length", defval = 12)
slow_length = input(title = "Slow Length", defval = 26)
src = input(title = "Source", defval = close)
signal_length = input.int(title = "Signal Smoothing",  minval = 1, maxval = 50, defval = 9, display = display.data_window)
sma_source = input.string(title = "Oscillator MA Type",  defval = "EMA", options = ["SMA", "EMA"], display = display.data_window)
sma_signal = input.string(title = "Signal Line MA Type", defval = "EMA", options = ["SMA", "EMA"], display = display.data_window)
// Calculating
fast_ma = sma_source == "SMA" ? ta.sma(src, fast_length) : ta.ema(src, fast_length)
slow_ma = sma_source == "SMA" ? ta.sma(src, slow_length) : ta.ema(src, slow_length)
macd = fast_ma - slow_ma
signal = sma_signal == "SMA" ? ta.sma(macd, signal_length) : ta.ema(macd, signal_length)
hist = macd - signal

// Calculate the upper and lower bands of the StdDev channel
stdDev = ta.stdev(macd, NumBars, false)
upperBand = 0 + (nStdDevs * stdDev)
lowerBand = 0 - (nStdDevs * stdDev)

// Plot the MACD line and the channel
plot(upperBand, title="Upper Band (N Std Dev)", color=#899289)
plot(lowerBand, title="Lower Band (N Std Dev)", color=#8b9c8c)

// Background color zones when MACD is outside the standard deviation bands
bgcolor(macd > upperBand ? color.new(color.purple, 75) : na, title="MACD Above Upper Band")
bgcolor(macd < lowerBand ? color.new(color.purple, 75) : na, title="MACD Below Lower Band")

// In the main chart
bgcolor(macd > upperBand ? color.new(color.purple, 90) : na, title="MACD Above Upper Band", force_overlay = true, editable = false)
bgcolor(macd < lowerBand ? color.new(color.purple, 90) : na, title="MACD Below Lower Band", force_overlay = true, editable = false)

alertcondition(hist[1] >= 0 and hist < 0, title = 'Rising to falling', message = 'The MACD histogram switched from a rising to falling state')
alertcondition(hist[1] <= 0 and hist > 0, title = 'Falling to rising', message = 'The MACD histogram switched from a falling to rising state')

hline(0, "Zero Line", color = color.new(#787B86, 50))
plot(macd, title = "MACD", color = #2962FF)
plot(signal, title = "Signal", color = #FF6D00)
