//@version=5
//-------------------------------------------------------------------------------------------------------------------------------------------
//  Script Name:    HSGP-I-MACD v3.0 Indicator (with N x StdDeviation)
//  Ideal      :    Custom version of MACD for use with GPX Strategies
//  Author     :    Guy Pedashenko      Original creation date: 15-Jun-24       Latest version date: 05-Jul-24
//  Version    :    v3.18
//  Description:    MACD crossing.  N x Std Devation band lines (can adjust variable for Std Dev multiplier.)
//                  Hayden updated for zone.
//                  Guy updated to include labels on Chart and Alerting
//                                                                       
//-------------------------------------------------------------------------------------------------------------------------------------------
indicator(title="HSGP-I-MACD V3", shorttitle="HSGP-MACDv3.18", timeframe="", timeframe_gaps=true)

NumBars = input.int(title = "Number of Bars",  minval = 100, maxval = 4000, defval = 2000)    
nStdDevs = input.float(title = "Number of STD Deviations",  minval = 1, maxval = 10, defval = 2)    
showLabelsOn = input.bool(true, title="Show Labels", group="MACD Settings", display = display.data_window)
bool macdOS = false
bool macdOB = false
bool Labelcontinue = false
bool MACDCrossSHORT = false
bool MACDCrossLONG = false

// Getting inputs
fast_length = input(title = "Fast Length", defval = 12)
slow_length = input(title = "Slow Length", defval = 26)
src = input(title = "Source", defval = close)
signal_length = input.int(title = "Signal Smoothing",  minval = 1, maxval = 50, defval = 9, display = display.data_window)
sma_source = input.string(title = "Oscillator MA Type",  defval = "EMA", options = ["SMA", "EMA"], display = display.data_window)
sma_signal = input.string(title = "Signal Line MA Type", defval = "EMA", options = ["SMA", "EMA"], display = display.data_window)
// Calculating
fast_ma = sma_source == "SMA" ? ta.sma(src, fast_length) : ta.ema(src, fast_length)
slow_ma = sma_source == "SMA" ? ta.sma(src, slow_length) : ta.ema(src, slow_length)
macd = fast_ma - slow_ma
signal = sma_signal == "SMA" ? ta.sma(macd, signal_length) : ta.ema(macd, signal_length)
hist = macd - signal

// Calculate the upper and lower bands of the StdDev channel
stdDev = ta.stdev(macd, NumBars, false)
upperBand = 0 + (nStdDevs * stdDev)
lowerBand = 0 - (nStdDevs * stdDev)

// Plot the MACD line and the channel
plot(upperBand, title="Upper Band (N Std Dev)", color=#899289)
plot(lowerBand, title="Lower Band (N Std Dev)", color=#8b9c8c)

if macd >= upperBand
	macdOB := true
if macd <= lowerBand
	macdOS := true

// Background color zones when MACD is outside the standard deviation bands
bgcolor(macd > upperBand ? color.new(color.purple, 75) : na, title="MACD Above Upper Band")
bgcolor(macd < lowerBand ? color.new(color.purple, 75) : na, title="MACD Below Lower Band")
// In the main chart
bgcolor(macd > upperBand ? color.new(color.purple, 90) : na, title="MACD Above Upper Band", force_overlay = true, editable = false)
bgcolor(macd < lowerBand ? color.new(color.purple, 90) : na, title="MACD Below Lower Band", force_overlay = true, editable = false)

hline(0, "Zero Line", color = color.new(#787B86, 50))
plot(macd, title = "MACD", color = #2962FF)
plot(signal, title = "Signal", color = #FF6D00)

// Labeling for MACD Oversold or Overbought
if showLabelsOn and macdOB and not macdOB[1]
	Labelcontinue := true
if showLabelsOn and macdOS and not macdOS[1]
	Labelcontinue := true
plotshape(Labelcontinue ? macdOB : na, title="MACD is Overbought", text=" McOB ", style=shape.labeldown, force_overlay = true, location=location.abovebar, color= #f01515, textcolor=color.white, size=size.small )
plotshape(Labelcontinue ? macdOS : na, title="MACD is OverSold", text=" McOS ", style=shape.labelup, force_overlay = true, location=location.belowbar, color= color.rgb(34, 197, 29), textcolor=color.white, size=size.small )
// ALERT
alertcondition((macdOB and Labelcontinue ) or (macdOS and Labelcontinue), title = 'MACD OB/OS', message = 'MACD is MACD OB/OS')

// Labeling for MACD Crossing
if macd <= signal and macd[1] > signal[1] and macdOB
    MACDCrossSHORT := true
if macd >= signal and macd[1] < signal[1] and macdOS
    MACDCrossLONG := true
if showLabelsOn and MACDCrossSHORT 
	Labelcontinue := true
if showLabelsOn and MACDCrossLONG
	Labelcontinue := true
plotshape(Labelcontinue ? MACDCrossSHORT : na, title="MACD Cross Sell", text=" McSell ", style=shape.labeldown, force_overlay = true, location=location.abovebar, color= color.rgb(133, 22, 177), textcolor=color.white, size=size.small )
plotshape(Labelcontinue ? MACDCrossLONG : na, title="MACD Cross Buy", text=" McBuy ", style=shape.labelup, force_overlay = true, location=location.belowbar, color= color.rgb(133, 22, 177), textcolor=color.white, size=size.small )
// ALERT
alertcondition(MACDCrossSHORT or MACDCrossLONG, title = 'MACD Crossing', message = 'MACD is Crossing')

//alertcondition(hist[1] >= 0 and hist < 0, title = 'Rising to falling', message = 'The MACD histogram switched from a rising to falling state')
//alertcondition(hist[1] <= 0 and hist > 0, title = 'Falling to rising', message = 'The MACD histogram switched from a falling to rising state')